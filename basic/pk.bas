5 INPUT "TYPE <ENTER> TO BEGIN"; SD$:SD=RND(-TI)
10 A0 = 64
15 A1 = 7
20 A2 = 7
25 A6 = (A1 * A2) * 8
30 DIM S0(A1,A2) :REM LAND OFFSET
35 DIM S1(A1,A2) :REM NATIVE OFFSET
40 DIM X(A0),Y(A0)
45 DIM DX(A0),DY(A0)
50 DIM CH(A0) :REM CHANGE FLAG
55 DIM L1(15),L2(15)
60 DIM YY(A1)
65 DIM A4$(12)
70 A4$(0)  = "WENDROS 1" :REM JAN
75 A4$(1)  = "WENDROS 2" :REM FEB
80 A4$(2)  = "WENDROS 3" :REM MAR
85 A4$(3)  = "HEWSREH 1" :REM SPRING
90 A4$(4)  = "HEWSREH 2" :REM SUMMER
95 A4$(5)  = "HEWSREH 3" :REM AUTUMN
100 A4$(6)  = "SAMREH 1 " :REM WINDER
105 A4$(7)  = "SAMREH 2 " :REM AUG
110 A4$(8)  = "SAMREH 3  " :REM SEP
115 A4$(9)  = "KERPOS 1  " :REM OCT
120 A4$(10) = "KERPOS 2  " :REM NOV
125 A4$(11) = "KERPOS 3  " :REM DEC
130 A5 = 500
135 A4 = 0
140 A3   = 1
145 PX = 134
150 PY = 30
155 COLOR 8,0 :REM 0=TRANSPARENT
160 ? CHR$(147)
165 POKE $9F29,($40 OR PEEK($9F29)) :REM ENABLE SPRITES
170 REM SHIP
175 S0 = $FC10
180 X = PX + 164
185 Y = PY + 164
190 BK = 2
195 LS = INT(RND(1)*8) * 32
200 FL = 0
205 PO = 0
210 VPOKE $F, S0+0, %00000000+LS :REM LSB
215 VPOKE $F, S0+1, %10000000+BK :REM R...BBBB
220 VPOKE $F, S0+2, X AND 255    :REM X
225 VPOKE $F, S0+3, X  / 256     :REM X MSB
230 VPOKE $F, S0+4, Y AND 255    :REM Y
235 VPOKE $F, S0+5, Y  / 256     :REM Y MSB
240 VPOKE $F, S0+6, %00001100+FL :REM COLL,ZZ,FLIP
245 VPOKE $F, S0+7, %10100000+PO :REM HHWW+PO
250 L1(0)=4   :L2(0)=0
255 L1(1)=4   :L2(1)=128
260 L1(2)=5   :L2(2)=0
265 L1(3)=5   :L2(3)=0
270 L1(4)=5   :L2(4)=0
275 L1(5)=5   :L2(5)=128
280 L1(6)=5   :L2(6)=128
285 L1(7)=5   :L2(7)=128
290 L1(8)=4   :L2(8)=128
295 L1(9)=6   :L2(9)=0
300 L1(10)=4  :L2(10)=128
305 L1(11)=6  :L2(11)=0
310 L1(12)=4  :L2(12)=0
315 L1(13)=6  :L2(13)=128
320 L1(14)=6  :L2(14)=128
325 L1(15)=6  :L2(15)=128
330 REM LAND AND NATIVES
335 ZZ = 0 :REM INDEX INTO BK() AND LS()
340 FL = 0 :REM FLIP?
345 FOR R = 0 TO A1-1
350    FOR C = 0 TO A2-1
355       SS = R * A1 + C + 1
360       S0 = $FC10 + SS * 8  :REM LAND OFFSET
365       S1 = S0 + A6 :REM NATIVE OFFSET
370       Y(SS) = R * 48
375       X(SS) = C * 48 
380       PO = 0 :REM PALETTE OFFSET
385       BK=L1(ZZ)
390       LS=L2(ZZ)
395       ZZ=ZZ+1 :IF ZZ=16 THEN ZZ=0
400       X = PX + X(SS)
405       Y = PY + Y(SS)
410       Z = 1 * 4 :REM 1=BACK,3=FRONT
415       S0(R,C) = S0
420       VPOKE $F, S0+0, %00000000+LS    :REM LSB
425       VPOKE $F, S0+1, %10000000+BK    :REM R...BBBB
430       VPOKE $F, S0+2, X AND 255       :REM X
435       VPOKE $F, S0+3, X  / 256        :REM X MSB
440       VPOKE $F, S0+4, Y AND 255       :REM Y
445       VPOKE $F, S0+5, Y  / 256        :REM Y MSB
450       VPOKE $F, S0+6, %00000000+Z+FL  :REM COLL,ZZ,FLIP
455       VPOKE $F, S0+7, %11110000+PO    :REM HHWW..PO
460       X = X+16
465       Y = Y+16
470       Z = 0 * 4 :REM SET TO 2 TO BE VISIBLE
475       S1(R,C) = S1
480       VPOKE $F, S1+0, %00000000+(BK-1)*16
485       VPOKE $F, S1+1, %10000011
490       VPOKE $F, S1+2, X AND 255
495       VPOKE $F, S1+3, X / 256
500       VPOKE $F, S1+4, Y AND 255
505       VPOKE $F, S1+5, Y / 256
510       VPOKE $F, S1+6, %00000000+Z     :REM COLL,ZZ,FLIP
515       VPOKE $F, S1+7, %10100000       :REM 32 X 32
520    NEXT C
525 NEXT R
530 REM ---------------------------------------
535 REM
540 REM  LOAD SHIPS INTO BANK 1
545 REM
550 REM ---------------------------------------
555 LOAD "XEBEC-32X32.BIN",       8, 1,$A000 
560 LOAD "DRAKKAR-32X32.BIN",     8, 1,$A400 
565 LOAD "DROMON-32X32.BIN",      8, 1,$A800 
570 LOAD "GENOESE-32X32.BIN",     8, 1,$AC00 
575 LOAD "SAMBUK-32X32.BIN",      8, 1,$B000 
580 LOAD "PENTEKONTOR-32X32.BIN", 8, 1,$B400 
585 LOAD "TARTANE-32X32.BIN",     8, 1,$B800 
590 LOAD "TRIREME-32X32.BIN",     8, 1,$BC00
595 REM ---------------------------------------
600 REM
605 REM  LOAD SETTLEMENTS INTO BANK 5
610 REM
615 REM ---------------------------------------
620 LOAD "CAMP-32X32-BW.BIN",     8, 5,$A000 
625 LOAD "VILLAGE-32X32-BW.BIN",  8, 5,$A400
630 LOAD "PUEBLO-32X32-BW.BIN",   8, 5,$A800
635 LOAD "AZTEC-32X32-BW.BIN",    8, 5,$AC00
640 LOAD "INCA-32X32-BW.BIN",     8, 5,$B000
645 REM ---------------------------------------
650 REM
655 REM  LOAD LAND INTO BANKS 7-9
660 REM
665 REM ---------------------------------------
670 LOAD "OCEAN.BIN",             8, 7,$A000 :REM 7A
675 LOAD "ISLAND-DESERT-X.BIN",   8, 7,$B000 :REM 7B
680 LOAD "ISLAND-GRASS-X.BIN",    8, 8,$A000 :REM 8A
685 LOAD "ISLAND-SAVANNAH-X.BIN", 8, 8,$B000 :REM 8B
690 LOAD "ISLAND-FOREST-X.BIN",   8, 9,$A000 :REM 9A
695 LOAD "ISLAND-MOUNTAIN-X.BIN", 8, 9,$B000 :REM 9B
700 REM ---------------------------------------
705 REM
710 REM  LOAD MAP INTO BANKS 10+
715 REM
720 REM ---------------------------------------
725 LOAD "MAP.BIN",               8,10,$A000 :REM BANK 10
730 REM ---------------------------------------
735 REM
740 REM  LOAD YOUR SHIP INTO VERA AT $4000
745 REM
750 REM ---------------------------------------
755 POKE $9F61,1
760 VPOKE %10000,$4000,0 :REM SET UP AUTOINCREMENT
765 FOR I = $A000 TO $BFFF
770    POKE $9F23,PEEK(I)
775 NEXT
780 REM ---------------------------------------
785 REM
790 REM  LOAD SETTLEMENTS INTO VERA AT $6000
795 REM
800 REM ---------------------------------------
805 POKE $9F61,5
810 VPOKE %10000,$6000,0 :REM SET UP AUTOINCREMENT
815 FOR I = $A000 TO $BFFF
820    POKE $9F23,PEEK(I)
825 NEXT
830 REM ---------------------------------------
835 REM
840 REM  LOAD OCEAN AND LAND INTO VERA AT $8000
845 REM
850 REM ---------------------------------------
855 VPOKE %10000,$8000,0 :REM RESET AUTOINCREMENT
860 FOR BB=7 TO 9 :REM BANKS 7 TO 9
865    POKE $9F61,BB :REM POINT TO THE RIGHT BANK
870    FOR I=$A000 TO $BFFF
875       POKE $9F23,PEEK(I)
880    NEXT
885 NEXT
890 DX=108
895 DY=118
900 GOSUB 1030
905 GET A$ :IF A$="" GOTO 905
910    IF A$ = "\X9D" THEN DX=DX-1 :FL=1
915    IF A$ = "\X1D" THEN DX=DX+1 :FL=0
920    IF A$ = "\X91" THEN DY=DY-1 
925    IF A$ = "\X11" THEN DY=DY+1 
930    IF A$ = "Q" THEN DX=DX-1:DY=DY-1 :FL=1
935    IF A$ = "Z" THEN DX=DX-1:DY=DY+1 :FL=1
940    IF A$ = "E" THEN DX=DX+1:DY=DY-1 :FL=0
945    IF A$ = "C" THEN DX=DX+1:DY=DY+1 :FL=0
950    IF A$ = "X" THEN COLOR 1,6 :?CHR$(147) :END
955    IF DX > 248 THEN DX = 248 :GOTO 905
960    IF DX < 0   THEN DX = 0   :GOTO 905
965    IF DY > 248 THEN DY = 248 :GOTO 905
970    IF DY < 0   THEN DY = 0   :GOTO 905
975    S0 = $FC10 :REM OFFSET
980    VPOKE $F, S0+6, %00001100+FL :REM FLIP SHIP
985 A3 = A3 + 1
990 IF A3 < 30 THEN 1015
995 A3 = 1 :A4 = A4 +1
1000 IF A4 = 12 THEN A4 = 0 : A5 = A5 +1
1005 ? CHR$(19); SPC(32); A4$(A4); A5
1010 ? SPC(32) DX "," DY
1015 GOSUB 1030
1020 GOTO 905
1025 END
1030 REM THE MAP IS 256 X 256
1035 BB=0
1040 FOR R = 0 TO A1-1
1045    Y = DY + R
1050    REM
1055    REM EACH BANK HAS 32 ROWS
1060    REM
1065    Y3 = INT(Y/32)
1070    BA = 10 + Y3
1075    IF BB <> BA THEN BB = BA :POKE $9F61,BA
1080    YY = 32 * (Y/32 - Y3)    :REM Y MOD 32
1085    FOR C = 0 TO A2-1
1090       LA = PEEK($A000+YY*256+DX+C) 
1095       LB = LA AND 240
1100       LA = LA AND 15
1105       BK=L1(LA)
1110       LS=L2(LA)
1115       S1 = S1(R,C)
1120       IF LB=0 THEN VPOKE $F,S1+6,0 :GOTO 1135
1125       VPOKE $F, S1, LB
1130       VPOKE $F, S1+6, %00001000 :REM ZZ=2
1135       S0 = S0(R,C)                    :REM OFFSET
1140       VPOKE $F, S0+0, %00000000+LS    :REM LSB
1145       VPOKE $F, S0+1, %10000000+BK    :REM R...BBBB
1150    NEXT C
1155 NEXT R
1160 RETURN
